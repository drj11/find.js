#!/usr/bin/env coffee

fs = require 'fs'
util = require 'util'
{spawn} = require 'child_process'

async = require 'async'
# https://github.com/melor/node-posix
posix = require 'posix'

IN = process.stdin
ERR = process.stderr
OUT = process.stdout
EXIT_STATUS = 0

evalExpression = (file, environment, node, cb) ->
  """*file* is an object with a .name property and a
  .stat method.
  *node* is a compiled node object, as returned by
  compileExpression."""
  switch node.op
    when '-depth'
      return cb true
    when '-exec;'
      return exec file, node, cb
    when '-exec+'
      return execAggregate file, node, cb
    when '-links'
      return file.stat (err, stat) ->
        x = node.arg
        cb compare stat.nlink, x
    when '-newer'
      return file.stat (err, stat) ->
        fs.stat node.arg, (err, f) ->
          cb stat.mtime > f.mtime
    when '-ok;'
      return ok file, node, cb
    when '-print'
      OUT.write file.name + '\n'
      return cb true
    when '-prune'
      environment.descend = false
      return cb true
    when '-size'
      return file.stat (err, stat) ->
        x = node.arg
        s = stat.size
        if /c$/.test x
          # Removing trailing 'c'.
          x = x[..-2]
        else
          # Measure in blocks (rounded up).
          s = Math.ceil(s / 512)
        cb compare s, x
    when '-type'
      return file.stat (err, stat) ->
        if node.arg == 'f'
          v = stat.isFile()
        if node.arg == 'd'
          v = stat.isDirectory()
        if node.arg == 'l'
          v = stat.isSymbolicLink()
        if node.arg == 'p'
          v = stat.isFIFO()
        return cb v
    when '-user'
      # :todo: move into compilation step.
      try
        user = posix.getpwnam node.arg
      catch error
        user = uid: +node.arg
      return file.stat (err, stat) ->
        cb(stat.uid == user.uid)
    when '-a'
      return evalExpression file, environment, node.arg[0], (res) ->
        if not res
          cb res
        else
          evalExpression file, environment, node.arg[1], cb
    when '-o'
      return evalExpression file, environment, node.arg[0], (res) ->
        if res
          cb res
        else
          evalExpression file, environment, node.arg[1], cb
  console.warn "Unknown expression", node
  return cb()

compare = (a, x) ->
  '''Compare the numeric attribute *a* of a file or directory
  with the command line argument to an operator, handling
  correctly the case of +n and -n.
  '''
  if x[0] == '+'
    return (a > +x)
  else if x[0] == '-'
    return (a < -x)
  else
    return (a == +x)

exec = (file, node, cb) ->
  args = []
  for item in node.arg
    if item == '{}'
      item = file.name
    args.push item
  child = spawn args[0], args[1..], stdio: 'inherit'
  child.on 'close', (status) ->
    # "the primary shall evaluate as true if the utility returns
    # a zero value as exit status"
    cb ! status

buffer = ''
afterRead = ->
makeLine = (cb) ->
  if '\n' in buffer
    line = buffer.match(/^.*\n/)[0]
    rest = buffer.replace /^.*\n/, ''
    buffer = rest
    afterRead = ->
    cb line
# Read one line from a stream. The line is passed to the cb function.
readLine = (stream, cb) ->
  readable = ->
    while chunk = stream.read()
      s = chunk.toString('binary')
      buffer += s
      afterRead()
  if not stream.readableSet
    stream.on 'readable', readable
    stream.readableSet = true
  afterRead = ->
    makeLine cb
  makeLine cb

ok = (file, node, cb) ->
  args = []
  for item in node.arg
    if item == '{}'
      item = file.name
    args.push item
  ERR.write args.join(' ') + '..?'
  readLine IN, (response) ->
    if /^y/.test response
      child = spawn args[0], args[1..], stdio: 'inherit'
      child.on 'close', (status) ->
        # "the primary shall evaluate as true if the utility returns
        # a zero value as exit status"
        cb ! status
    else
      """the command shall not be invoked and the value of the -ok
      operand shall be false"""
      cb false

execAggregate = (file, node, cb) ->
  """We should aggregate here, but we're going to just exec once
  per file."""
  args = []
  for item in node.arg
    if item == '{}'
      item = file.name
    args.push item
  child = spawn args[0], args[1..], stdio: 'inherit'
  child.on 'close', (status) ->
    # "If any invocation returns a non-zero value as exit
    # status, the find utility shall return a non-zero exit
    # status"
    if status
      EXIT_STATUS = status
    # "the primary shall always evaluate as true"
    cb true

class File
  constructor: (@name) ->

  stat: (cb) ->
    if @_stat
      return cb null, @_stat
    # :todo: stat vs lstat should be under control of -H and
    # -L options.
    fs.lstat @name, (err, stat) =>
      if not err
        @_stat = stat
      cb err, stat

# Not really a compiler, but there are some source to source
# transformations we do to add an implicit -print.
compileExpression = (tokenList) ->
  environment = {}
  if tokenList.length == 0
    tokenList.push '-print'
  if '-print' not in tokenList and '-exec' not in tokenList and '-ok' not in tokenList
    tokenList = ['('].concat tokenList, [')', '-print']
  # Set .depth when -depth is used.
  # :todo: ideally we'd remove these '-depth' tokens, but we
  # can't do that easily as it might upset the syntax (for
  # example, 'find . -type -a -depth' is valid)
  environment.depth = ('-depth' in tokenList)
  [parsed, tail] = parseExpression tokenList
  if /parse/.test process.env.FIND_DEBUG
    console.warn util.inspect parsed, depth: Infinity
  return { environment: environment, tree: parsed }

# operator precedence table
PREC =
  o: 4
  a: 8

parsePrec = (expression, precedence) ->
  if /descent/.test process.env.FIND_DEBUG
    console.warn expression
  if expression[0] == '('
    [L, tail] = parseExpression expression[1..]
    if tail[0] != ')'
      throw 'mismatched paren ' + expression + tail
    tail.shift()
  else if expression[0] in ['-name', '-path', '-perm',
    '-type', '-links', '-user', '-group',
    '-size', '-atime', '-ctime', '-mtime',
    '-newer']
    # All the 1-ary operators.
    L = { op: expression[0], arg: expression[1] }
    tail = expression[2..]
  else if expression[0] in ['-nouser', '-nogroup',
    '-xdev', '-prune', '-print', '-depth']
    # All the 0-ary operators.
    L = { op: expression[0] }
    tail = expression[1..]
  else if expression[0] in ['-exec', '-ok']
    L = null
    # Consume up to ';' or ['{}', '+']
    # L.op will set to either '-exec;' or '-exec+'
    for i in [1..expression.length-1]
      if expression[i] == ';'
        L = { op: expression[0]+';', arg: expression[1..(i-1)] }
        tail = expression[(i+1)..]
        break
      if expression[i] == '{}' and expression[i+1] == '+'
        L = {op: expression[0]+'+', arg: expression[1..i] }
        tail = expression[(i+2)..]
        break
    if not L
      throw "bad #{expression[0]} #{expression}"
  reduceTail L, tail, precedence

reduceTail = (L, tail, precedence) ->
  if tail.length == 0
    return [L, tail]
  if tail[0] == ')'
    return [L, tail]
  if tail[0] == '-o'
    if precedence <= PREC.o
      [R, tail] = parsePrec tail[1..], PREC.o
      node = { op: '-o', arg: [L, R] }
      return reduceTail node, tail, precedence
    else
      return [L, tail]
  if tail[0] == '-a'
    # -a is optional between expressions.
    tail = tail[1..]
  if precedence <= PREC.a
    [R, tail] = parsePrec tail, PREC.a
    node = { op: '-a', arg: [L, R] }
    return reduceTail node, tail, precedence
  else
    return [L, tail]

parseExpression = (exp) ->
  parsePrec exp, 0
  

one = (file, expression, cb) ->
  environment = expression.environment
  environment.descend = true
  evalExpression file, environment, expression.tree, ->
    file.stat (err, st) ->
      if err
        throw err
      if environment.descend and st.isDirectory()
        mapdir file, expression, cb
      else
        cb()

oneDepth = (file, expression, cb) ->
  file.stat (err, st) ->
    if err
      throw err
    if st.isDirectory()
      mapdir file, expression, ->
        evalExpression file, expression.environment, expression.tree, cb
    else
      evalExpression file, expression.environment, expression.tree, cb

mapdir = (file, expression, done) ->
  fs.readdir file.name, (err, names) ->
    if err
      throw err
    async.eachLimit names, 1, (name, cb) ->
      newname = pathjoin file.name, name
      one (new File(newname)), expression, cb
    , done

pathjoin = (dir, name) ->
  if /\/$/.test dir
    return dir + name
  else
    return dir + "/" + name

parseArguments = (args) ->
  files = []
  expression = []
  for v,i in args
    # The first argument that is '!' or ')' or starts with a '-'
    # marks the start of the expression.
    if /^(-|!$|\($)/.test v
      expression = args[i..]
      break
    else
      files.push v
  return [files, expression]

[files, expression] = parseArguments process.argv[2..]

compiled = compileExpression expression
if compiled.environment.depth
  one = oneDepth

async.each files, (name, cb) ->
  one (new File(name)), compiled, cb
, -> process.exit EXIT_STATUS
