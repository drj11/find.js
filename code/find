#!/usr/bin/env coffee

fs = require 'fs'
async = require 'async'

OUT = process.stdout

evalExpression = (name, node, cb) ->
  """*node* is a compiled node object, as returned by
  compileExpression."""
  switch node.op
    when '-print'
      OUT.write name + '\n'
      return cb true
    when '-size'
      return fs.stat name, (err, stat) ->
        s = Math.ceil(stat.size / 512)
        x = node.arg
        if x[0] == '+'
          cb(s > +x)
        else if x[0] == '-'
          cb(s < -x)
        else
          cb(s == +x)
    when '-type'
      return fs.stat name, (err, stat) ->
        if node.arg == 'f'
          v = stat.isFile()
        if node.arg == 'd'
          v = stat.isDirectory()
        return cb v
    when '-a'
      return evalExpression name, node.arg[0], (res) ->
        if not res
          cb res
        else
          evalExpression name, node.arg[1], cb
    when '-o'
      return evalExpression name, node.arg[0], (res) ->
        if res
          cb res
        else
          evalExpression name, node.arg[1], cb
  console.warn "Unknown expression", node
  return cb()

# Not really a compiler, but there are some source to source
# transformations we do to add an implicit -print.
compileExpression = (expression) ->
  if expression.length == 0
    expression.push '-print'
  if '-print' not in expression and '-exec' not in expression and '-ok' not in expression
    expression = ['('].concat expression, [')', '-print']
  [parsed, tail] = parseExpression expression
  return parsed

parseAnd = (expression) ->
  if /descent/.test process.env.FIND_DEBUG
    console.warn expression
  if expression[0] == '('
    [L, tail] = parseExpression expression[1..]
    if tail[0] != ')'
      throw 'mismatched paren' + expression
    tail.shift()
  else if expression[0] in ['-name', '-path', '-perm',
    '-type', '-links', '-user', '-group',
    '-size', '-atime', '-ctime', '-mtime',
    '-newer']
    # All the 1-ary operators.
    L = { op: expression[0], arg: expression[1] }
    tail = expression[2..]
  else if expression[0] in ['-nouser', '-nogroup',
    '-xdev', '-prune', '-print', '-depth']
    # All the 0-ary operators.
    L = { op: expression[0] }
    tail = expression[1..]
  if tail.length == 0
    return [L, tail]
  if tail[0] == ')'
    return [L, tail]
  if tail[0] == '-o'
    [R, tail] = parseAnd tail[1..]
    node = { op: '-o', arg: [L, R] }
    return [node, tail]
  if tail[0] == '-a'
    # -a is optional between expressions.
    tail = tail[1..]
  [R, tail] = parseAnd tail
  node = { op: '-a', arg: [L, R] }
  return [node, tail]

parseExpression = parseAnd
  

one = (name, expression, cb) ->
  evalExpression name, expression, ->
    fs.stat name, (err, st) ->
      if err
        throw err
      if st.isDirectory()
        mapdir name, expression, cb
      else
        cb()

mapdir = (dir, expression, done) ->
  fs.readdir dir, (err, names) ->
    if err
      throw err
    async.each names, (name, cb) ->
      newname = pathjoin dir, name
      one "#{newname}", expression, cb
    , done

pathjoin = (dir, name) ->
  if /\/$/.test dir
    return dir + name
  else
    return dir + "/" + name

parseArguments = (args) ->
  files = []
  expression = []
  for v,i in args
    # The first argument that is '!' or ')' or starts with a '-'
    # marks the start of the expression.
    if /^(-|!$|\($)/.test v
      expression = args[i..]
      break
    else
      files.push v
  return [files, expression]

[files, expression] = parseArguments process.argv[2..]

compiled = compileExpression expression

async.each files, (name, cb) ->
  one name, compiled, cb
, process.exit
